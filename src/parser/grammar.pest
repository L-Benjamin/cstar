// Quiet rules

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }


// Common definitions

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
void = { "void" }
bool = { "true" | "false" }
int = @{ (ASCII_DIGIT+ | "0x" ~ ASCII_HEX_DIGIT+ | "0b" ~ ASCII_BIN_DIGIT+) }
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ("e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
char = @{ "'" ~ "\\"? ~ ANY ~ "'" }
string = @{ "\"" ~ (("\\" | !"\"") ~ ANY)* ~ "\"" }


// Files

program = { SOI ~ element* ~ init ~ run ~ EOI }
init = { "Init" ~ "[" ~ (ident ~ ",")* ~ ident? ~ "]" ~ ";" }
run = { "Run" ~ "[" ~ (ident ~ ",")* ~ ident? ~ "]" ~ ";" }


// Elements

element = { component |  resource | function | struct_ | system }
component = { "Component" ~ ident ~ struct_def }
resource = { "Resource" ~ ident ~ struct_def }
struct_ = { "struct" ~ ident ~ struct_def }
system = { "System" ~ ident ~ filter_list ~ block }
function = { "function" ~ ident ~ "(" ~ (ident ~ ",")* ~ ident? ~ ")" ~ block }


// Types

void_t = { "void" } bool_t = { "bool" } int_t = { "int" }
float_t = { "float" } char_t = { "char" } string_t = { "string" }
entity_t = { "entity" }
type_ = { void_t | bool_t | int_t | float_t | char_t | string_t | entity_t | ident }


// Curly braces and blocks

struct_def = { "{" ~ (type_ ~ ident ~ ";")* ~ "}" }
block = { "{" ~ stmt* ~ "}" }


// Filters and queries

arg = { ident ~ ident }
entity_filter = { "Entity" ~ ident ~ ("," ~ arg)* ~ ","? }
resource_filter = { arg }
filter = { entity_filter | resource_filter }
filter_list = { "(" ~ (filter ~ ";")* ~ filter? ~ ")" }


// Builtins and calls

clone = { "Clone" } delete = { "Delete" }
spawn = { "Spawn" } print = { "print" } println = { "println" }
builtin = { clone | delete | spawn | println | print }
call = { (builtin | ident) ~ "(" ~ (expr ~ ",")* ~ expr? ~ ")" }


// L-values

access = { ident ~ ("." ~ ident)+ }
lvalue = { access | ident }


// Values

assign = { lvalue ~ "=" ~ expr }
atom = { void | bool | float | int | char | string }
struct_init = { ident ~ "{" ~ (ident ~ ":" ~ expr ~ ";")* ~ "}" }
value = { atom | call | struct_init | assign | lvalue }


// Binary operators and expressions

binop = _{ add | sub | mul | div | mod_ | and | or | xor | bitand | bitor | shl | shr | leq | geq | lt | gt | eq | neq }
add = { "+" }  sub = { "-" }  mul = { "*" }  div = { "/" }    mod_ = { "%" }
and = { "&&" } or = { "||" }  xor = { "^" }  bitand = { "&" } bitor = { "|" }
shl = { "<<" } shr = { ">>" } leq = { "<=" } geq = { ">=" }   lt = { "<" }
gt = { ">" }   eq = { "==" }  neq = { "!=" }
binexpr = { term ~ (binop ~ term)+ }


// Unary operators and expressions

unop = { pos | neg | not | bitnot }
pos = { "+" } neg = { "-" } not = { "!" } bitnot = { "~" }
unexpr = { unop ~ term }


// Terms and expressions

ternary = { "(" ~ expr ~ ")" ~ "?" ~ expr ~ ":" ~ expr }
term = { value | unexpr | "(" ~ expr ~ ")" }
expr = { binexpr | ternary | term }


// Statements

if_ = { "if" ~ "(" ~ expr ~ ")" ~ block ~ ("else" ~ block)? }
for_ = { "for" ~ "(" ~ (decl | expr) ~ ";" ~ expr ~ ";" ~ expr ~ ")" ~ block }
while_ = { "while" ~ "(" ~ expr ~ ")" ~ block }
query = { "query" ~ filter_list ~ block }
switch = { "switch" ~ "(" ~ expr ~ ")" ~ "{" ~ ("case" ~ atom ~ ":" ~ block)* ~ "default" ~ ":" ~ block ~ "}" }
break_ = { "break" }
continue_ = { "continue" }
return_ = { "return" ~ expr? }
decl = { "let" ~ ident ~ ("=" ~ expr)? }
stmt = { if_ | for_ | while_ | query | switch | block | (break_ | continue_ | return_ | decl | expr) ~ ";" | ";" }