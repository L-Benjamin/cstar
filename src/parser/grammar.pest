// Quiet rules

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }


// Common definitions

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
void = { "void" }
bool = { "true" | "false" }
int = @{ (ASCII_DIGIT+ | "0x" ~ ASCII_HEX_DIGIT+ | "0b" ~ ASCII_BIN_DIGIT+) }
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ("e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
char = @{ "'" ~ "\\"? ~ ANY ~ "'" }
string = @{ "\"" ~ (("\\" | !"\"") ~ ANY)* ~ "\"" }


// Files

program = { SOI ~ element* ~ init ~ run ~ EOI }
init = { "Init" ~ "[" ~ (ident ~ ",")* ~ ident? ~ "]" ~ ";" }
run = { "Run" ~ "[" ~ (ident ~ ",")* ~ ident? ~ "]" ~ ";" }


// Elements

element = { component | resource | system }
component = { "Component" ~ ident ~ struct_def }
resource = { "Resource" ~ ident ~ struct_def }
system = { "System" ~ ident ~ filter_list ~ block }


// Curly braces and blocks

struct_def = { "{" ~ (ident ~ ";")* ~ "}" }
block = { "{" ~ stmt* ~ "}" }


// Filters and queries

const_ = { "const" }
arg = { const_? ~ ident ~ ident }
entity_filter = { "Entity" ~ ident ~ ("," ~ arg)* ~ ","? }
resource_filter = { arg }
filter = { entity_filter | resource_filter }
filter_list = { "(" ~ (filter ~ ";")* ~ filter? ~ ")" }


// Builtins and calls

builtin = { clone | delete | spawn | print }
clone = { "Clone" } delete = { "Delete" }
spawn = { "Spawn" } print = { "Print" }
call = { builtin ~ "(" ~ (expr ~ ",")* ~ expr? ~ ")" }


// L-values

access = { ident ~ ("." ~ ident)+ }
lvalue = { access | ident }


// Values

assign = { lvalue ~ "=" ~ expr }
atom = { void | bool | int | float | char | string }
struct_init = { ident ~ "{" ~ (ident ~ ":" ~ expr ~ ";")* ~ "}" }
value = { atom | call | struct_init | assign | lvalue }


// Binary operators and expressions

binop = { add | sub | mul | div | mod_ | and | or | xor | bitand | bitor | shl | shr | leq | geq | lt | gt | eq | neq }
add = { "+" }  sub = { "-" }  mul = { "*" }  div = { "/" }    mod_ = { "%" }
and = { "&&" } or = { "||" }  xor = { "^" }  bitand = { "&" } bitor = { "|" }
shl = { "<<" } shr = { ">>" } leq = { "<=" } geq = { ">=" }   lt = { "<" }  
gt = { ">" }   eq = { "==" }  neq = { "!=" }
binexpr = { term ~ (binop ~ term)+ }


// Unary operators and expressions

unop = { pos | neg | not | bitnot }
pos = { "+" } neg = { "-" } not = { "!" } bitnot = { "~" }
unexpr = { unop ~ term }


// Terms and expressions

term = { value | unexpr | "(" ~ expr ~ ")" }
expr = { binexpr | term }


// Statements

if_ = { "if" ~ "(" ~ expr ~ ")" ~ block ~ ("else" ~ block)? }
for_ = { "for" ~ "(" ~ expr? ~ ";" ~ expr? ~ ";" ~ expr? ~ ")" ~ block }
while_ = { "while" ~ "(" ~ expr ~ ")" ~ block }
query = { "query" ~ filter_list ~ block }
break_ = { "break" }
continue_ = { "continue" }
stmt = { if_ | for_ | while_ | query | block | (expr | break_ | continue_) ~ ";" | ";" }